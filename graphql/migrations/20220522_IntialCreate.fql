Let(
    {
        index_params: {
            name: "allBalances",
            source: Collection("Balance")
        }
    },
    If(
        Exists(Index("allBalances")),
        Update(Index("allBalances"), Var("index_params")),
        CreateIndex(Var("index_params"))
    )
)

Let(
  {
    function_params: {
      name: "balanceByAddress",
      body: Query(
          Lambda(
            ["address", "size", "before", "after"],
            Let(
              {
                filter: Filter(
                  Match(Index("allBalances")),
                  Lambda(
                    "x",
                    ContainsStr(LowerCase(Select(["data", "address"], Get(Var("x")))), LowerCase(Var("address")))
                  )
                ),
                page: If(
                  Equals(Var("before"), null),
                  If(
                    Equals(Var("after"), null),
                    Paginate(Var("filter"), { size: Var("size") }),
                    Paginate(Var("filter"), { after: Var("after"), size: Var("size") })
                  ),
                  Paginate(Var("filter"), { before: Var("before"), size: Var("size") })
                )
              },
              Map(Var("page"), Lambda("x", Get(Var("x"))))
            )
          )
        )
    }
  },
  If(
    Exists(Function("balanceByAddress")),
    Update(Function("balanceByAddress"), Var("function_params")),
    CreateFunction(Var("function_params"))
  )
)